plugins {
    id 'codenarc'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.gradle.plugin-publish' version '0.14.0'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'se.patrikerdes.use-latest-versions' version '0.2.13'
}

sourceCompatibility = 1.8

dependencies {
    implementation gradleApi()
    implementation 'net.sf.saxon:Saxon-HE:9.8.0-14'
    implementation 'xml-resolver:xml-resolver:1.2'

    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'org.spockframework:spock-junit4:2.0-groovy-3.0'

    testImplementation gradleTestKit()
}

group = 'com.github.eerohele'
version = '0.9.0-beta3'

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

wrapper {
    gradleVersion = '7.1.1'
}

codenarc {
    toolVersion = '0.24.1'
    configFile = file('config/codenarc/codenarc.groovy')
}

pluginBundle {
    website = 'https://github.com/eerohele/saxon-gradle'
    vcsUrl = 'https://github.com/eerohele/saxon-gradle.git'
    description = 'A Gradle plugin for running Saxon'
    tags = ['saxon', 'xslt', 'documentation']
}

gradlePlugin {
    plugins {
        SaxonPlugin {
            id = 'com.github.eerohele.saxon-gradle'
            displayName = 'Saxon Gradle Plugin'
            implementationClass = 'com.github.eerohele.SaxonPlugin'
            description = 'A Gradle plugin for running XSLT transformations with Saxon'
        }
    }
}

publishPlugins.dependsOn check

// Temporary workaround for phantom duplicates in Gradle 7+.
rootProject.tasks.named("processResources") {
    duplicatesStrategy = 'include'
}

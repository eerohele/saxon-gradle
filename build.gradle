plugins {
    id 'groovy'
    id 'codenarc'
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'java-gradle-plugin'
    id 'se.patrikerdes.use-latest-versions' version '0.2.17'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

sourceCompatibility = 1.8

dependencies {
    implementation gradleApi()
    implementation 'net.sf.saxon:Saxon-HE:10.5'
    implementation 'xml-resolver:xml-resolver:1.2'

    testImplementation('org.spockframework:spock-core:2.0-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }

    testImplementation 'junit:junit:4.13.2'
    testImplementation gradleTestKit()
}

repositories {
    mavenCentral()
}

group = 'com.github.eerohele'
version = '0.9.0-beta3'

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

wrapper {
    gradleVersion = '7.2'
}

codenarc {
    toolVersion = '0.24.1'
    configFile = file('config/codenarc/codenarc.groovy')
}

pluginBundle {
    website = 'https://github.com/eerohele/saxon-gradle'
    vcsUrl = 'https://github.com/eerohele/saxon-gradle.git'
    description = 'A Gradle plugin for running Saxon'
    tags = ['saxon', 'xslt', 'documentation']
}

gradlePlugin {
    plugins {
        SaxonPlugin {
            id = 'com.github.eerohele.saxon-gradle'
            displayName = 'Saxon Gradle Plugin'
            implementationClass = 'com.github.eerohele.SaxonPlugin'
            description = 'A Gradle plugin for running XSLT transformations with Saxon'
        }
    }
}

publishPlugins.dependsOn check

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

plugins {
    id 'groovy'
    id 'codenarc'
//    id 'com.gradle.plugin-publish' version '0.14.0'
//    id 'java-gradle-plugin'
    id "maven-publish"
    id "signing"
}

sourceCompatibility = 1.8

dependencies {
    implementation gradleApi()
    implementation 'net.sf.saxon:Saxon-HE:10.5'
    implementation 'xml-resolver:xml-resolver:1.2'

    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }

    testImplementation gradleTestKit()
}

repositories {
    mavenCentral()
}

group = 'com.nwalsh'
version = '0.9.1-beta1'

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

wrapper {
    gradleVersion = '7.0'
}

codenarc {
    toolVersion = '2.1.0'
    configFile = file('config/codenarc/codenarc.groovy')
}

/*
pluginBundle {
    website = 'https://github.com/ndw/saxon-gradle'
    vcsUrl = 'https://github.com/ndw/saxon-gradle.git'
    description = 'A Gradle plugin for running Saxon'
    tags = ['saxon', 'xslt', 'documentation']
}

gradlePlugin {
    plugins {
        SaxonPlugin {
            id = 'com.nwalsh.saxon-gradle'
            displayName = 'Saxon Gradle Plugin'
            implementationClass = 'com.nwalsh.SaxonPlugin'
            description = 'A Gradle plugin for running XSLT transformations with Saxon'
        }
    }
}
*/

//publishPlugins.dependsOn check

tasks.register("groovyDocsJar", Jar) {
  dependsOn "groovydoc"
  from groovydoc.destinationDir
  archiveClassifier = "javadoc"
}

tasks.register("sourcesJar", Jar) {
  from sourceSets.main.allSource
  archiveClassifier = "sources"
}

//tasks.findByName("publishMavenJavaPublicationToMavenRepository").dependsOn "signPluginMavenPublication"

signing {
  sign publishing.publications
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'Saxon Gradle Plugin'
        description = 'A Gradle plugin for running XSLT transformations with Saxon'
        url = 'https://github.com/ndw/saxon-gradle'

        scm {
          url = 'scm:git@github.com:ndw/saxon-gradle.git'
          connection = 'scm:git@github.com:ndw/saxon-gradle.git'
          developerConnection = 'scm:git@github.com:ndw/saxon-gradle.git'
        }

        licenses {
          license {
            name = 'The MIT License (MIT)'
            url = 'http://opensource.org/licenses/MIT'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'ndw'
            name = 'Norman Walsh'
          }
        }
      }
      groupId = 'com.nwalsh'
      artifactId = 'saxon-gradle'
      version = version

      from components.java
      artifact sourcesJar
      artifact groovyDocsJar
    }
  }

  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = findProperty("sonatypeUsername") ?: ""
        password = findProperty("sonatypePassword") ?: ""
      }
    }
  }
}
